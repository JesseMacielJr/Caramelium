// A linguagem não possui suporte para ponto flutuando, então todos os cálculos
// serão feitos com números inteiros com esse valor de escala.
is := 65536;
y := 0;
$lin: { 
    y >= 146 ? <- $lin;
    x := 0;
    $col: {
        x >= 150 ? <- $col;
        cr := x * (is / 64) - (is*5/3);
        ci := y * (is / 64) - (is*9/8);

        i := 0;
        j := 0;
        iter := 0;

        $while: { 
            !(i * i + j * j < 4*is*is && iter < 255) ? <- $while;
            ni := (i * i) / is - (j * j) / is + cr;
            j = 2 * (i * j) / is + ci;
            i = ni;
            iter += 1;
            -> $while;
        };
        //  .:-=+*#%@
        c := $char: {
            iter <= 2 ? "@"
            : iter == 3 ? "%%"
            : iter == 4 ? "#"
            : iter == 5 ? "*"
            : iter == 6 ? "+"
            : iter == 7 ? "="
            : iter == 8 ? "-"
            : iter == 9 ? ":"
            : iter == 10 ? "."
            : iter == 255 ? " "
            : { iter -= 13; -> $char; }
        };
        escrever(c);
        x += 1;
        -> $col;
    };
    y += 2;
    escreverln("");
    -> $lin;
};
